---
title: Мелкие радости - расчет отклонений
date: 2020-03-05
image: images/blog/blog_010_foto.jpg
author: Smile
---

> *Рвав-рвав, сегодня я -- фитнес-собака!*
>
> *Похоже, вопрос дизайна незримо присутствует в окружающих меня вещах, и это будет уже 2-я заметка в этом году (из 2-х!), на эту тему :-)* 
>
> *Перед вами -- 6-я заметка из нашего цикла "Мелкие радости", и сегодня мы рассмотрим часто встречающиеся запросы по визуализации сравнения плана и факта в наиболее простом его варианте - таблице. На эту тему написано довольно много, и, в принципе, тема не особо сложна.*


**Пример:**

В качестве исходных данных имеется набор, содержащий вид товара, а также плановую и фактическую суммы.

![blog_010_screen_1](https://kkadikin.ru/images/blog/blog_010_screen_1.jpg)


**Задача:**

На основе представленных данных реализовать следующие требования:

**<li>** посчитать абсолютное отклонение факта от плана;

**<li>** посчитать относительное отклонение факта от плана;

**<li>** отформатировать данные таким образом, чтобы положительные значения отображались со знаком "+", а отрицательные со знаком "-";

**<li>** визуализировать при помощи иконок направление движения показателей;

**<li>** раскрасить значения в разные цвета;

**<li>** объединить получившийся результат в один столбец.


> *Рвав-рвав, как уже отмечалось выше, все довольно просто, но муторно.* 
>
И прежде, чем приступать к разработке, необходимо ответить себе на следующие вопросы:

**<li>** С помощью чего будем считать показатели? Реализация возможна как на мерах, так и на расчетных столбцах. Меры -- выбор редакции.

**<li>** Как будет осуществляться форматирование данных, а именно -- появление знаков "+" и "-"?. В данном случае собака Смайл пойдет простым путем, и нарисует их текстом, но особо привередливые человеки могут использовать функцию FORMAT.

**<li>** Что делать с иконками? Иконки можно вставлять как картинку, или использовать символы Unicode. Символы -- выбор редакции.

**<li>** Каким способом, собственно, красить-то будем? Понятное дело, что надо использовать условное форматирование, но вот цвета и условия можно указывать либо при помощи встроенных возможностей, либо задавать при помощи меры. Опять же, меры -- выбор редакции.


**Реализация требований:**

**<li>** Для формирования конечного столбца нам понадобятся некоторые предварительные расчеты, а именно, несколько простых мер следующего вида:

```dax
План =
SUM ( 'Таблица'[Плановая сумма] )
```

```dax
Факт =
SUM ( 'Таблица'[Фактическая сумма] )
```

```dax
Абсолютное отклонение =
[Факт] - [План]
```

```dax
Относительное отклонение =
DIVIDE ( [Абсолютное отклонение]; [План]; 0 )
```

![blog_010_screen_2](https://kkadikin.ru/images/blog/blog_010_screen_2.jpg)


**<li>** Следующим шагом создание меры для цветового обозначения, кодировку цветов, естественно, нужно предварительно подобрать:

```dax
Цвет = 
SWITCH (
    TRUE ();
    [Абсолютное отклонение] > 0; "#009900";
    [Абсолютное отклонение] < 0; "#ff0000";
    [Абсолютное отклонение] = 0; "#ffff00"
)
```

**<li>**  Далее необходимо собрать получишийся результат в один столбец, с добавлением определенных символов Unicode и форматирования.

Формула, позволяющая получить составное значение, выглядит следующим образом:

```dax
Результат = 
VAR _AbsoluteDeviation =
    IF ( 
        [Абсолютное отклонение] > 0; "+" & [Абсолютное отклонение]; [Абсолютное отклонение] 
    )
VAR _RelativeDeviation =
    IF (
        [Относительное отклонение] > 0;
        "+" & FORMAT ( [Относительное отклонение]; "0.0%" );
        FORMAT ( [Относительное отклонение]; "0.0%" )
    )
VAR _UnicodeSquare = 9724
VAR _UnicodeTriangleUp = 9650
VAR _UnicodeTriangleDown = 9660
VAR _UnicodeLable =
    SWITCH (
        TRUE ();
        [Абсолютное отклонение] = 0; UNICHAR ( _UnicodeSquare );
        [Абсолютное отклонение] > 0; UNICHAR ( _UnicodeTriangleUp );
        [Абсолютное отклонение] < 0; UNICHAR ( _UnicodeTriangleDown );
        BLANK ()
    )
VAR _UnicodeLongSpace = 8194
VAR _Gap =
    UNICHAR ( _UnicodeLongSpace )
RETURN
    _AbsoluteDeviation & _Gap & _RelativeDeviation & _Gap & _UnicodeLable
```

![blog_010_screen_3](https://kkadikin.ru/images/blog/blog_010_screen_3.jpg)

**<li>**  Последним шагом является применение условного форматирования на основе меры "Цвет":

![blog_010_screen_4](https://kkadikin.ru/images/blog/blog_010_screen_4.jpg)

> *Рвав-рвав, женский пол с наступающим праздником!*
>
> *Ваш Смайл*